;This is Pirate Hook's house.
@#PIRATE
@LABEL PIRMENU
@CLEAR SCREEN
@SHOW
`8Pirate Hook

     `%How can I help you?


@DO `V01 IS 1
@do write
`x`x
@CHOICE
`%Who are you?
+`t01 3 `#About that delivery job...
>`p46 0 `$Intermail
`%Leave
@IF RESPONCE IS 1 THEN DO
@BEGIN
@CLEAR SCREEN
@SHOW
`$
     I am `8Hook`$, the great pirate!

     Before the `4Maraken`$ was established here, I ruled all local commerce
     from the pirate's station in the `4Maraken Clouds`$.

     Now that they took over, I've been forced to live here & offer delivery
     services to prospective buyers.

@KEY
@BITSET `t01 3 1
@DO GOTO PIRMENU
@END

@IF RESPONCE IS 2 THEN DO
@BEGIN
@do `p20 is 2
@run getquest in quest.ref
@END

@IF RESPONCE IS 3 THEN DO
@BEGIN
@CLEAR SCREEN
@SHOW
                              `9Planetary Intermail

@if USERDAT\`p12.TXT exist false then do
 @begin
 @writefile USERDAT\`p12.TXT
  `3Barnicle: `$Hello!
 @end
@if USERDAT\`p12.TXT exist true then do
 @begin
 @displayfile USERDAT\`p12.TXT
 @end
@show
@
(Do tricky stuff with name to determine how much they can type)
@DO `s09 is `%
@DO `s09 add `n
@DO STRIP `s09 <-- Strips all none color ` codes out
@DO `s09 add `x`1:`x`9
@DO `p25 is length `s09
@DO `p26 is 76
@DO `p26 - `p25
@DO WRITE
  `1Enter your message or `9ENTER`1 to abort.`\  `s09
@DO READSTRING `p26 NIL `s10 <--Read string, NIL is default. (nothing)
@do stripbad `s10
@SHOW

@DO `p27 is length `s10
@IF `p27 less 3 then do
 @begin
 @DO GOTO PIRMENU
 @end
 @writefile USERDAT\`p12.TXT
  `%`n`1: `9`s10
 @do trim USERDAT\`p12.TXT 17
@DO GOTO PIRMENU
@END

@IF RESPONCE IS 4 THEN DO
@ROUTINE END



;The Maraken Center
@#MARAKEN
     ;Creates roster if non-existant.
@if USERDAT\MMEMBER.DAT exist false then do
 @begin
 @writefile USERDAT\MMEMBER.DAT
  `4Maraken Roster

 @end

     ;The Grounds
@LABEL MM1
@CLEAR SCREEN
@PAUSEOFF
@DISPLAYFILE ANSI\EBASE.ANS
@SHOW
`9Where do you want to go?

@PAUSEON
@DO `V01 IS 1
@do write
`x`x
@CHOICE
`4Recruiting Office
`4The Pit
`4Leave
@IF RESPONCE IS 1 THEN DO
@BEGIN
@DO GOTO MM2
@END
@IF RESPONCE IS 2 THEN DO
@BEGIN
@SHOW

`4The Pit's closed, but the prostitutes are still around.

@KEY
@DO GOTO MM1
@END
@IF RESPONCE IS 3 THEN DO
@BEGIN
@ROUTINE END
@END

     ;Recruiting Office
@LABEL MM2
@CLEAR SCREEN
@PAUSEOFF
@DISPLAYFILE ANSI\EHEAD.ANS
@SHOW
`9Where do you want to go?

@PAUSEON
@DO `V01 IS 1
@do write
`x`x
@CHOICE
>`i30 0 !`p19 2 `$Apply for membership.
`4See the membership roster.
-`t01 4 `@Buy a uniform (Needed to join).
`4Leave
@IF RESPONCE IS 1 THEN DO
@BEGIN
;The induction

@CLEAR SCREEN
@SHOW

`4You face many officers & Carnage himself.

@KEY
@CLEAR SCREEN
@PAUSEOFF
@DISPLAYFILE ANSI\CARNAGE.ANS
@SHOW
`4Do you swear to follow our great order until the end?
Type AGREE if you do!

@PAUSEON
@DO READSTRING 5 NIL `s03
@IF `s03 IS AGREE THEN DO
 @BEGIN
 @CLEAR SCREEN
 @DO `p19 IS 2
 @writefile USERDAT\MMEMBER.DAT
  `%`N
 @SHOW

`4Welcome to our great order!


 @KEY
 @DO GOTO MM2
 @END
@IF `s03 NOT AGREE THEN DO
 @BEGIN
 @CLEAR SCREEN
 @SHOW

`4You hath rejected us! Go now from this place!


 @KEY
 @DO GOTO MM2
 @END
@END

@IF RESPONCE IS 2 THEN DO
;The roster
@BEGIN
@CLEAR SCREEN
@DISPLAYFILE USERDAT\MMEMBER.DAT
@KEY
@DO GOTO MM2
@END

@IF RESPONCE IS 3 THEN DO
;The uniform
@BEGIN
@CLEAR SCREEN
@SHOW

`9You might want to buy a uniform!

`9It's only `$5000`9 gold!

`@Y `9is Yes
`8N `9is No

@DO READSPECIAL `s01 YN
@IF `s01 IS Y THEN DO
 @BEGIN
 @IF money LESS 5000 THEN DO
  @BEGIN
  @SHOW

`9You can't afford the `4Maraken Uniform`9. Rats!

  @KEY
  @END
 @IF money MORE 4999 THEN DO
  @BEGIN
  @DO money - 5000
  @DO `i30 + 1
  @BITSET `t01 4 1
  @SHOW

`9You don an `4Maraken Uniform`9. How spiffy!

  @KEY
  @END
 @END
@IF `s01 IS N THEN DO
 @BEGIN
 @SHOW

`9You can't afford the `4Maraken Uniform`9. Rats!

 @KEY
 @END
@DO GOTO MM2
@END

@IF RESPONCE IS 4 THEN DO
@BEGIN
@DO GOTO MM1
@END


@#EXIT
@DO SAYBAR
`4Maraken troops are training today. No one may leave the city!
@DO MOVEBACK

@#END
@drawmap
@do moveback
@update
@routineabort
